package flat/meta

import flat/operators/EqualsOperator

class Class<Type> {
  visible Bool isInterface

  visible String location
  visible String package => location.substring(0, location.lastIndexOf("/", defaultReturnValue: 0))
  visible String name => location.substring(location.lastIndexOf("/") + 1)

  visible static Class[] ALL = Array()

  public construct(
    visible String location,
    visible Bool isInterface,
    visible Class extension,
    visible Class[] interfaces
  ) {}

  public isOfType(Class other) =>
    (other.isInterface ?
      isOfTypeInterface(other) || extension && extension.isOfType(other) :
      isOfTypeClass(other) || interfaces.any({ _.isOfTypeClass(other) })) ||
      Object.referenceEquals(other, Object.class) ||
      Object.referenceEquals(other, EqualsOperator.class)

  public isOfTypeClass(Class other) => false {
    var current = this

    while (current) {
      if (Object.referenceEquals(current, other)) {
        return true
      }

      current = current.extension
    }
  }

  public isOfTypeInterface(Class other) => false {
    var current = this

    while (current) {
      if (current.interfaces.any(i => i == other || i.isOfTypeInterface(other))) {
        return true
      }

      current = current.extension
    }
  }

  public getClassesOfType(Class baseType) => ALL.filter({ _.isOfType(baseType) })

  override public equals(Object other) => (other || !this) && other.class.isOfType(Class.class) && equals((Class)other)
  public equals(Class another) => this.location.chars.data == another.location.chars.data

  public toString() => "Class \"#location\""
}